=LET(
    moveseq_regex,          "[UuFfRrBbLlDdxyzMES'2 ]+",
    split_left,             LAMBDA(x, sep,
                                TRIM(LEFT(RIGHT(x, LEN(x) - 1), FIND(sep, x) - 2))
                            ),
    split_right,            LAMBDA(x, sep,
                                TRIM(RIGHT(LEFT(x, LEN(x) - 1), LEN(x) - 1 - FIND(sep, x)))
                            ),
    invert_move,            LAMBDA(move,
                                IFS(
                                    REGEXMATCH(move, "[UuFfRrBbLlDdxyzMES]'"), LEFT(move, LEN(move) - 1),
                                    REGEXMATCH(move, "[UuFfRrBFRBLDxyzMES]2"), move,
                                    REGEXMATCH(move, "[UuFfRrBFRBLDxyzMES]"),  CONCAT(move, "'")
                                )
                            ),
    invert_moveseq,         LAMBDA(moves,
                                TRIM(REDUCE("", SPLIT(moves, " "), LAMBDA(acc, m, CONCATENATE(invert_move(m), " ", acc))))
                            ),
    invert_alg,             LAMBDA(alg, rec,
                                IFS(
                                    REGEXMATCH(alg, CONCATENATE("^", moveseq_regex, "$")),
                                    invert_moveseq(alg),
                                    REGEXMATCH(alg, CONCATENATE("^\[", moveseq_regex, ",", moveseq_regex, "\]$")),
                                    CONCATENATE("[", split_right(alg, ","), ", ", split_left(alg, ","), "]"),
                                    REGEXMATCH(alg, CONCATENATE("^\[", moveseq_regex, ":.*\]$")),
                                    LET(
                                        setup, split_left(alg, ":"),
                                        inner, split_right(alg, ":"),
                                        CONCATENATE("[", setup, " : ", rec(inner, rec), "]")
                                    ),
                                    TRUE,
                                    LET(
                                        moves, INDEX(Algorithms!$B$2:$B, MATCH(alg, Algorithms!$A$2:$A, 0)),
                                        invert_moveseq(moves)
                                    )
                                )
                            ),
    x,                      TRIM(INDEX($B$2:$V$22, MATCH(B$1, $B$1:$V$1, 0), MATCH($A3, $B$1:$V$1, 0))),
    IF(
        x = "",
        "",
        invert_alg(x, invert_alg)
    )
)
